<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="UTF-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0" />
    <title>Listing 7-8, Lighting with point light and attenuation.</title>
    <script src="./app.js" defer></script>
    <script src="./glMatrix.js" defer></script>
    <script src="./webgl-debug.js" defer></script>
    <script src="./webgl-utils.js" defer></script>

    <script id="shader-vs" type="x-shader/x-vertex">
      attribute vec3 aVertexPosition;
      attribute vec3 aVertexNormal;
      attribute vec2 aTextureCoordinates;

      uniform mat4 uMVMatrix;
      uniform mat4 uPMatrix;
      uniform mat3 uNMatrix;

      uniform vec3 uLightPosition;

      varying vec2 vTextureCoordinates;
      varying vec3 vNormalEye;
      varying vec3 vPositionEye3;
      varying vec3 vLightPositionEye3;

      void main() {
        vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition, 1.0);
        vPositionEye3 = vertexPositionEye4.xyz / vertexPositionEye4.w;

        vec4 lightPositionEye4 = uMVMatrix * vec4(uLightPosition, 1.0);
        vLightPositionEye3 = lightPositionEye4.xyz / lightPositionEye4.w;

        vNormalEye = normalize(uNMatrix * aVertexNormal);

        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        vTextureCoordinates = aTextureCoordinates;
      }
    </script>

    <script id="shader-fs" type="x-shader/x-fragment">
      precision mediump float;

      varying vec2 vTextureCoordinates;
      varying vec3 vNormalEye;
      varying vec3 vPositionEye3;
      varying vec3 vLightPositionEye3;

      uniform vec3 uAmbientLightColor;
      uniform vec3 uDiffuseLightColor;
      uniform vec3 uSpecularLightColor;

      uniform sampler2D uSampler;

      const float shininess = 32.0;
      const float constantAtt = 1.0;
      const float linearAtt = 0.1;
      const float quadraticAtt = 0.05;

      vec3 lightWeighting = vec3(0.0, 0.0, 0.0);

      void main() {
        float att = 0.0;

        vec3 vectorToLightSource = normalize(vLightPositionEye3 - vPositionEye3);

        float diffuseLightWeighting = max(dot(vNormalEye, vectorToLightSource), 0.0);

        if (diffuseLightWeighting > 0.0) {
          float distance = length(vec3(vLightPositionEye3 - vPositionEye3));

          att = 1.0 / (constantAtt + linearAtt * distance + quadraticAtt * distance * distance);

          vec3 reflectionVector = normalize(reflect(-vectorToLightSource, vNormalEye));

          vec3 viewVectorEye = -normalize(vPositionEye3);

          float rdotv = max(dot(reflectionVector, viewVectorEye), 0.0);

          float specularLightWeighting = pow(rdotv, shininess);

          lightWeighting =
            att * uDiffuseLightColor * diffuseLightWeighting +
            att * uSpecularLightColor * specularLightWeighting;
        }

        lightWeighting += uAmbientLightColor;

        vec4 texelColor = texture2D(uSampler, vTextureCoordinates);
        gl_FragColor = vec4(lightWeighting.rgb * texelColor.rgb, texelColor.a);
      }
    </script>
  </head>
  <body onload="startup();">
    <canvas id="myGLCanvas" width="500" height="500"></canvas>
  </body>
</html>

<!-- 
  7-7 예제 버텍스 셰이더와 차이점


  이전 예제에서의 버텍스 셰이더와 대부분 동일하지만 차이점이 하나 있음.

  1. 바로 uLightPosition 유니폼 변수를 프래그먼트 셰이더가 아닌, 버텍스 셰이더에서 받는 점!
  2. 또, app.js의 setupLights() 함수에서 받아온 uLightPosition 의 벡터값을 눈좌표계로 변환하는 계산도 해줘야 함!

  왜 이렇게 하냐면, 이전 예제에서는
  uLightPosition을 가지고 프래그먼트 셰이더에서 
  코사인값을 구하는 내적 계산 및 반사벡터를 구하는 계산에 필요한
  광원벡터(vectorToLightSource) 값을 구하는 데에만 사용했음.

  근데 사실 이전 예제에서 저 광원벡터는 각도만 정확하면 되고,
  길이값은 어차피 normalize() 함수를 이용해서 
  정규화된 벡터를 사용할거니까 정확한 길이값을 몰라도 상관이 없었지.


  그런데, 7-7 예제에서는, 광원벡터를 구하는 데에도 사용되긴 하지만, 
  uLightPosition으로 구한 광원벡터의 '정확한 길이값'을 알아야 하는 상황인거지.
  왜냐하면, 감쇄량을 정확히 계산하려면 
  (광원 - 각 프래그먼트/버텍스) 사이의 거리값을 정확히 알고있어야 하니까!

  그래서 프래그먼트 셰이더에서 거리값을 계산하는 코드
  float distance = length(vec3(vLightPositionEye3 - vPositionEye3));
  에서 사용되는 vLightPositionEye3 는
  '버텍스 셰이더 내에서 눈좌표계로 변환하는 계산을 거치고 나서 넘겨준 광원의 위치값'
  이라고 보면 됨.

  정확한 계산을 위해서는, 각 버텍스/프래그먼트의 위치값도 눈좌표계로 변환되어야 하지만,
  동시에 광원의 위치값도 눈좌표계로 변환되어야 하는 것이지!

  또한, varying값으로 넘겨줘서 광원의 위치값이 보간된다고 하더라도,
  각 버텍스마다 app.js에서 쏴준 동일한 광원의 위치값을 넘겨주고 있기 때문에,
  버텍스마다 동일한 값이 varying 변수로 넘어가면 모두 동일한 값으로 보간되기 때문에

  vLightPositionEye3 값은 사실상 상수값처럼 사용할 수 있으므로, 
  프래그먼트마다 값이 달라지는 건 아니니 걱정하지 않아도 됨.
-->

<!-- 
  7-7 예제 프래그먼트 셰이더와 차이점


  이 예제는 '점 광원'에서 '빛의 감쇄'를 구현하는 예제이므로,
  프래그먼트 셰이더에서 감쇄량인 att(attenuation의 약자)값을 계산해주는 코드가 추가됨.
  구체적으로 추가된 내용은 아래와 같음.


  1. 거리 감소 함수에 사용될 감쇄 상수값들 지정
  
  const float constantAtt = 1.0;
  const float linearAtt = 0.1;
  const float quadraticAtt = 0.05;

  위의 세 값들은 점 광원에서 조명의 감쇄량을 계산할 때 사용하는
  '거리 감소 함수'에 사용되는 상수값들이다.

  각각 감쇄 상수, 선형 감쇄, 이차 감쇄 라고 하며,
  여기에 특정 값들을 할당해놓고, main() 함수에서 감쇄값인 att를 구할 때 사용하게 된다. p.362 참고.


  2. 감쇄량 계산 (feat. 거리값을 왜 프래그먼트 셰이더에서 계산할까?)

  float distance = length(vec3(vLightPositionEye3 - vPositionEye3));

  위의 코드는 감쇄량 계산에 필요한 (광원 - 각 버텍스/프래그먼트)의 거리값을 구하는 코드임.
  이 때, 광원의 위치값으로 보간된 변수인 vLightPositionEye3을 사용하는 것을 볼 수 있는데,
  위에 버텍스 셰이더에 관한 설명에서도 이야기 했듯이, 
  이 값은 모든 버텍스가 동일한 값으로 지정받기 때문에, 보간을 하더라도 값이 동일함.
  따라서 이전 예제에서 uLightPosition 유니폼 변수를 사용하는 것처럼 상수값으로 사용할 수 있다.

  또한, 어떻게보면 사실상 광원벡터에 해당하는데, 광원벡터의 정확한 거리값, 즉 길이를 알아야 하므로,
  셰이더의 내장함수 length()를 이용하면 손쉽게 이 길이를 계산할 수 있다.

  중요한 건, 이 때 항상 버텍스 셰이더에서 
  눈좌표계로 변환된 광원의 위치값을 이용해서 계산해야 한다는 점이다!

  또한, 감쇄량은 책 p.363의 그림 7.20을 보면 선형적으로 감소하지 않는데 비해
  프래그먼트 셰이더에서는 말 그대로 '선형보간'으로 선형적으로 보간된 값을 사용하기 때문에,
  버텍스 셰이더에서 감쇄량을 계산한 뒤, 프래그먼트 셰이더에서 보간하면 정확한 감쇄량을 구할 수 없으니
  당연히 감쇄량 att는 프래그먼트 셰이더에서 계산하는 게 맞음.
  
  그런데 왜, 거리값인 distance도 프래그먼트 셰이더에서 계산하는걸까?
  
  그 이유는 p.365에 잘 정리가 되어있음.
  결론부터 말하면, 광원과 버텍스들의 거리가 동일할 경우,
  거리값을 버텍스 셰이더에서 계산하고 프래그먼트 셰이더로 넘겨서 보간해버리면
  각 프래그먼트들과 광원의 거리값이 모두 똑같아지는 오류가 발생함.

  그런데 p.365에 그림 7.21만 봐도 알겠지만,
  실제로 광원이 모든 프래그먼트 및 버텍스와 거리값이 동일하지가 않잖아.
  다 다르잖아. 그런데 보간되서 계산된 거리값은 모두 똑같게 나오니까, 
  그 거리값으로 계산된 감쇄량도 모두 똑같아지겠지. -> 이는 명백한 오류지!

  따라서, 각 프래그먼트마다 광원과의 거리값을 정확히 계산하기 위해서,
  distance 값도 프래그먼트 셰이더에서 직접 계산해줄 수밖에 없음.


  3. att값을 디퓨즈 성분과 스펙큘러 성분에만 곱한 뒤, 총 밝기값에 합산해 줌.
  
  이거는 스포트 광원과도 유사하다고 볼 수 있음.
  스포트 광원에서도 spotEffect(원뿔에서의 빛 감소량) 값의 영향을 받는 성분은
  디퓨즈 성분, 스펙큘러 성분만 있기 때문에 두 성분에만 spotEffect를 곱해줬던 것과 같음.

  마찬가지로 점 광원에서 감쇄 효과의 영향을 받는 성분은
  디퓨즈 성분, 스펙큘러 성분이기 때문에, 
  두 성분에만 감쇄량인 att 값을 곱해준 뒤, 총 밝기값인 lightWeighting에 합산해준 것.

  물론 반사광을 재현한 성붐인 앰비언트 성분(uAmbientLightColor)는
  감쇄량과 상관없이 모든 프래그먼트/버텍스 들의 밝기값이 갖고 있어야 하므로,
  모든 프래그먼트 / 버텍스 들에 대해 합산해 줌.
-->
